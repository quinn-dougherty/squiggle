@precedence {
    func @cut,
    deref,
    call,
    unary @right,
    exp @right,
    times @left,
    plus @left,
    rel @left,
    and @left,
    or @left,
    control @right,
    else @right,
    logop @right
}

@top Program {
    Import*
    (
        statement
      | semicolon
    )*
    (expression)?
}

blockContent {
    (
        statement
      | semicolon
    )*
    (expression)
}

Import { import String as identifier }

commaSep<content> {
    "" | content ("," content)*
}

// when trailing comma is allowed
commaSep1<content> { "" | content ("," content?)* }

Binding { export? VariableName { identifier } "=" expression }

LambdaParameter {
    LambdaParameterName { identifier } (":" expression)?
}

LambdaArgs {
    () | LambdaParameter ("," LambdaParameter)*
}

FunDeclaration { export? FunctionName { identifier } ~callOrDeclaration "(" LambdaArgs ")" "=" expression }

statement[@isGroup="Statement"] {
    Binding
    | FunDeclaration
}

expression[@isGroup="Expression"] {
    String
    | Boolean
    | Number
    | BlockExpr { "{" blockContent "}" }
    | DictExpr {
        "{"
            commaSep1<
              Entry { Field[@dynamicPrecedence=1] { expression } ~inheritAmbig ":" expression } 
            | InheritEntry { Field[@dynamicPrecedence=0] { identifier } ~inheritAmbig }
            >
        "}"
    }
    | LambdaExpr { "{" ArgsOpen { "|" } LambdaArgs "|" blockContent "}" }
    | IfExpr { if expression then expression !else else expression }
    | ParenExpr { "(" expression ")" }
    | IdentifierExpr { identifier }
    | AccessExpr { expression !deref "." Field { identifier } }
    | CallExpr { expression ~callOrDeclaration !call "(" commaSep<Argument { expression }> ")" }
    | TernaryExpr { expression !logop LogicOp<"?"> expression LogicOp<":"> expression }
    | KVAccessExpr { expression !call ("[" Key { expression } "]") }
    | ArrayExpr { "[" commaSep1<expression> "]" }
    | UnaryExpr { !unary (ArithOp<"-"> | ArithOp<"!"> | DotArithOp<".-">) expression }
    | LogicExpr {
          expression !or     LogicOp<"||">  expression
        | expression !and    LogicOp<"&&">  expression
        | expression !rel    LogicOp<">">   expression
        | expression !rel    LogicOp<"<">   expression
        | expression !rel    LogicOp<"<=">  expression
        | expression !rel    LogicOp<">=">  expression
        | expression !rel    LogicOp<"==">  expression
    }
    | ControlExpr {
          expression !control  ControlOp<"->">  expression
    }
    | ArithExpr {
          expression !times  ( ArithOp<"*"> | DotArithOp<".*"> )                expression
        | expression !times  ( ArithOp<"/"> | DotArithOp<"./"> )                expression
        | expression !exp    ( ArithOp<"^"> | DotArithOp<".^"> )                expression
        | expression !plus   ( ArithOp<"+"> | DotArithOp<".+"> )                expression
        | expression !plus   ( ArithOp<"-"> | DotArithOp<".-"> )                expression
        | expression !plus     @extend[@name="ArithOp"]<identifier, "to">       expression
    }
}



Boolean { @specialize[@name="Boolean"]<identifier, "true" | "false"> }

kw<term> { @specialize[@name={term}]<identifier, term> }
if { kw<"if"> }
then { kw<"then"> }
else { kw<"else"> }
import { kw<"import"> }
export { kw<"export"> }
as { kw<"as"> }
@skip { spaces | newline | Comment | BlockComment }

@local tokens {
    blockCommentEnd { "*/" }
    blockCommentNewline { "\n" }
    @else blockCommentContent
}
@skip {} {
    BlockComment[@name="Comment"] { '/*' (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}
@skip {} {
    String { '"' (char|"'"|Escape {"\\"escape})* '"' | "'" (char|'"'|Escape {"\\"escape})* "'" }
}

@tokens {
    Comment { ( "#" | "//" ) ![\n]* }
    spaces[@export] { $[ ]+ }
    newline[@export] { $[\n] }
    semicolon[@export] { $[;] }

    char { $[\n\u{20}\u{21}\u{23}-\u{26}\u{28}-\u{5b}\u{5d}-\u{10ffff}] }
    escape { $['"\\\/bfnrt] | "u" hex hex hex hex }
    hex  { $[0-9a-fA-F] }
    
    "{" "}" "[" "]" "(" ")" "," "." "="[@name="Equals"]

    ArithOp<expr> { expr }
    DotArithOp[@name="ArithOp"]<expr> { expr }
    ControlOp<expr> { expr }
    LogicOp<expr> { expr }

    identifierChar { @asciiLetter | $[_$\u{a1}-\u{10ffff}] }
    word { identifierChar (identifierChar | @digit)* }
    identifier { word }

    Number {
        (@digit (@digit)* ("." (@digit)*)? | "." (@digit)*) (("e" | "E") ("+" | "-")? (@digit)+)? (identifier)?
    }
    
    @precedence { DotArithOp "." Number }
    @precedence { Comment '/*' ArithOp  }
}
